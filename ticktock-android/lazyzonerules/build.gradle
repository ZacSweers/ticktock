/*
 * Copyright (c) 2020 Zac Sweers
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *   https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
import dev.zacsweers.ticktock.gradle.GenerateZoneRuleFilesTask

plugins {
  id 'com.android.library'
  id 'com.vanniktech.maven.publish'
  id 'dev.zacsweers.ticktock'
}

android {
  compileSdkVersion 29
  compileOptions {
    targetCompatibility = JavaVersion.VERSION_1_8
    sourceCompatibility = JavaVersion.VERSION_1_8
  }
}

Provider<Directory> generatedDir = layout.buildDirectory.dir("generated/ticktock")

tickTock {
  codeOutputDir.set(generatedDir)
  tzOutputDir.set(layout.projectDirectory.dir("src/main/assets"))
  packageName.set("dev.zacsweers.ticktock.android.lazyzonerules")
}

android.libraryVariants.configureEach {
  it.registerJavaGeneratingTask(tasks.getByName("generateLazyZoneRules"), generatedDir.get().asFile)
}

mavenPublish {
  nexus {
    groupId = "dev.zacsweers"
  }
}

dependencies {
  tickTockCompiler project(":ticktock-compiler")
  api project(":ticktock-android:runtime")
  implementation project(":ticktock-runtime-lazyzonerules")
}
