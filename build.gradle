/*
 * Copyright (C) 2020 Zac Sweers & Gabriel Ittner
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *   https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
buildscript {
  dependencies {
    // Needs to go first, else gradle's forced 1.3.72 version takes precedence
    classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:1.4.21")
    classpath("dev.zacsweers.ticktock:ticktock-gradle-plugin:${findProperty("VERSION_NAME")}")
    classpath("com.android.tools.build:gradle:4.1.1")
  }
}

plugins {
  id 'org.jetbrains.kotlin.jvm' apply false version '1.4.21'
  id 'org.jetbrains.kotlin.kapt' apply false version '1.4.21'
  id 'com.diffplug.spotless' apply false version '5.8.2'
  id 'com.vanniktech.maven.publish' apply false version '0.13.0'
  id 'org.jetbrains.dokka' apply false version '1.4.20'
  id("binary-compatibility-validator") version "0.3.0"
}

subprojects {
  apply from: rootProject.file('gradle/dependencies.gradle')

  repositories {
    mavenCentral()
    google()
    exclusiveContent {
      forRepository {
        maven {
          name = "JCenter"
          setUrl("https://jcenter.bintray.com/")
        }
      }
      filter {
        // Required for Dokka
        it.includeModule("org.jetbrains.kotlinx", "kotlinx-html-jvm")
        it.includeGroup("org.jetbrains.dokka")
        it.includeModule("org.jetbrains", "markdown")
        // For lint
        it.includeModule("org.jetbrains.trove4j", "trove4j")
      }
    }
  }

  pluginManager.withPlugin('java-library') {
    java {
      sourceCompatibility = JavaVersion.VERSION_1_8
      targetCompatibility = JavaVersion.VERSION_1_8
    }
  }

  pluginManager.withPlugin('org.jetbrains.kotlin.jvm') {
    compileKotlin {
      kotlinOptions {
        jvmTarget = deps.build.kotlin.jvmTarget
        freeCompilerArgs += deps.build.kotlin.defaultFreeCompilerArgs
      }
      // TODO gradle is awful and I have no idea how to force a newer kotlin version to use this
//      kotlin {
//        explictApi()
//      }
    }

    compileTestKotlin {
      kotlinOptions {
        jvmTarget = deps.build.kotlin.jvmTarget
        freeCompilerArgs += deps.build.kotlin.defaultFreeCompilerArgs
      }
    }
  }

  pluginManager.withPlugin('org.jetbrains.dokka') {
    tasks.named("dokkaHtml") {
      it.outputDirectory.set(rootProject.file("docs/0.x"))
      it.dokkaSourceSets.configureEach {
        it.skipDeprecated.set(true)
      }
    }
  }

  apply from: rootProject.file('gradle/config-spotless.gradle')
}

apply from: 'gradle/dependencies.gradle'
